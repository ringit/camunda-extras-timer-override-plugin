plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

dependencies {
    implementation(platform("org.camunda.bpm:camunda-bom:7.13.0"))

    implementation("org.camunda.bpm:camunda-engine-spring")

    testImplementation("org.junit.jupiter:junit-jupiter:5.6.2")
    testImplementation("org.assertj:assertj-core:3.16.1")
    testImplementation("org.mockito:mockito-core:3.3.3")
    testImplementation("org.mockito:mockito-junit-jupiter:3.3.3")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'timer-override-plugin'
            from components.java
            pom {
                name = 'Timer Override Plugin'
                description = 'A Spring Boot starter for Camunda Process Engine Timer Override Plugin'
                url = 'https://github.com/ringit/camunda-extras-timer-override-plugin'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://choosealicense.com/licenses/mit/'
                    }
                }
                developers {
                    developer {
                        id = 'infeligo'
                        name = 'Mihhail Verhovtsov'
                        email = 'mihhail.verhovtsov@ringit.ee'
                    }
                    developer {
                        id = 'virtual-machinist'
                        name = 'Igor Volkov'
                        email = 'igor.volkov@ringit.ee'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ringit/camunda-extras-timer-override-plugin.git'
                    developerConnection = 'scm:git:ssh://github.com/ringit/camunda-extras-timer-override-plugin.git'
                    url = 'https://github.com/ringit/camunda-extras-timer-override-plugin'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

